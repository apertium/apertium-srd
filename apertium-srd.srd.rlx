DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" "<:>";
SOFT-DELIMITERS = "<,>" "<;>";
OPTIONS += no-inline-templates ; 
SUBREADINGS = LTR ; # Alternate, left-to-right (main reading on the left)

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) sent;

LIST A = adj ;
LIST Adv = adv ;
LIST PreAdv = preadv ;
LIST Pron = prn ;
LIST N = n ;
LIST Nsg = (n sg) ;
LIST Npl = (n pl) ;
LIST Acr = acr ;
LIST N_A = n adj ;
LIST NP = np ;
LIST V = vblex ;
LIST Vall = vblex vbmod vbser vbhaver ;
LIST Vconj = (vblex p1) (vblex p2) (vblex p3) ;	# verb conjugat
LIST Ser = vbser ;
LIST Haver = vbhaver ;
LIST Vmod = vbmod ;
LIST Vlex = vblex ;
LIST Inf = inf ;
LIST Ger = ger ;
LIST PP = pp ;
LIST Ifi = ifi ;
LIST Prs = prs ;
LIST Pis = pis ;
LIST Subj = prs pis ;
LIST Imper = imp ;
LIST Det = det ;
LIST DetDef = (det def) ;
LIST Def = def ;
LIST Ind = ind ;
LIST Dem = dem ;
LIST Pos = pos ;
LIST PreDet = predet ;
LIST CnjCoo = cnjcoo ;
LIST CnjSub = cnjsub ;
LIST CnjAdv = cnjadv ;
LIST Interj = ij ;
LIST Interr = itg ;
LIST Num = num ;
LIST Prep = pr ;
LIST Rel = rel ;
LIST CM = cm ;
LIST Lpar = lpar ;
LIST Guio = guio ;
LIST Excl = "!" ;


SET Noun_Adj = N OR A ;
SET Noun_Adj_PP = N OR A OR PP ;
SET Adj_PP = A OR PP ;
SET Noun_NP = N OR NP ;

SET Adv_PreAdv = Adv OR PreAdv ;

SET Ant = (np ant) ;
SET Cog = (np cog) ;
SET Ant_Cog = Ant OR Cog ;
SET Top = (np top) OR (np loc) OR (np hyd) ;
SET Org = (org) ;
SET Al = (al) ;

LIST Sg = sg ;
LIST Pl = pl ;
LIST SP = sp ;
LIST M = m ;
LIST F = f ;
LIST MF = mf ;

LIST Masc = m mf ;
LIST Fem = f mf ;
LIST SgSp = sg sp ;
LIST PlSp = pl sp ;
SET MascSg = (m sg) OR (m sp) OR (mf sg) OR (mf sp) ;
SET MascPl = (m pl) OR (m sp) OR (mf pl) OR (mf sp) ;
SET FemSg = (f sg) OR (f sp) OR (mf sg) OR (mf sp) ;
SET FemPl = (f pl) OR (f sp) OR (mf pl) OR (mf sp) ;

LIST P1 = (p1) ;
LIST P2 = (p2) ;
LIST P3 = (p3) ;
SET Sg1 = (p1 sg) ;
SET Sg2 = (p2 sg) ;
SET Sg3 = (p3 sg) ;
SET Pl1 = (p1 pl) ;
SET Pl2 = (p2 pl) ;
SET Pl3 = (p3 pl) ;
SET Pers = (p1) OR (p2) OR (p3) ;

SET ConcordaGN = MascSg OR MascPl OR FemSg OR FemPl;
SET ConcordaSP = SgSp OR PlSp ;
SET ConcordaGV = Sg1 OR Sg2 OR Sg3 OR Pl1 OR Pl2 OR Pl3 ;

SET Ref = (prn pro ref) ;
SET Pro = (prn pro) ;
SET Enc = (prn enc) ;
SET Tn = (prn tn) ;
SET Tn_nt = (prn tn nt) ;

SET Pref = (pref) ; 

SET Aord = (adj ord) OR ("sette") ;	# sette/setta

LIST unknown = ("\\*.*"r) ;
LIST unknownMajIni = ("\\*[A-ZÀÁÈÉíÒÓÚÜÇ].*"r) ;
LIST unknownMinIni = ("\\*[a-zàáäâèéëêíïîòóöôúüûç].*"r) ;
LIST MajIni = ("<[A-ZÀÁÈÉíÒÓÚÜÇ].*>"r) ;
LIST MinIni = ("<[a-zàáèéìíòóùúçñ].*>"r) ;
LIST MajIni2 = ("<[A-ZÀÁÈÉÍÌÒÓÚÜÇ][a-zàáèéìíòóùúïüçñ·]+.*>"r) ;
LIST segles_num_romans = ("[IVX][IVX]*"r) ;
LIST Anno = ("[1-2][0-9][0-9][0-9]"r) ;
LIST Upper = "[A-Z][a-z]+"r ;


LIST Bi = "<bi>"i ;
LIST Cale = "cale" ;
LIST Cantu = "cantu" ;
LIST Chi = "chi" ;
LIST Chie = "chie" ;
LIST Comente = "comente" ;
LIST Custu = "custu" ;
LIST Dare = "dare" ;
LIST Dae = "dae" ;
LIST Ddu = "<ddu>"i "<dda>"i "<ddos>"i "<ddas>"i "<dd'>"i "<dd'>"i "<dd’>"i ;
LIST De = "de" ;
LIST Est = "<est>"i ;
LIST In = "in" ;
LIST InTotu = "<in totu>"i ;
LIST Intre = "intre" ;
LIST Ite = "ite" ;
LIST ProIte = "pro ite" ;
LIST Li = "li" "ddi" ;
LIST Lu = "lu" "ddu" ;
LIST Meda = "<meda>"i ;
LIST Megius = "mègius" ;
LIST Mesu = "mesu" ;
LIST Peus = "peus" ;
LIST Nde = "nde" ;
LIST Nche = "nche" ;
LIST Ovest = "ovest" ;
LIST Prus = "prus" ;
LIST Tra = "tra" ;
LIST Si = "<si>"i ;
LIST Suo = "suo" ;
LIST Ue = "ue" ;

LIST Aere = ("àere" vblex) ;
LIST Cunsiderare = "cunsiderare" ;
LIST Istare = "istare" ;
LIST Giudicare = "giudicare" "giuigare" ;
LIST Sentire = "sentire" ;
LIST Resurtare = "resurtare" ;
LIST Mantenere = "mantènnere" ;
LIST Abarrare = "abarrare" ;
LIST Parrere = "pàrrere" ;

LIST AdjN = ("printzipale") ("ùrtimu") ("àteru") ("matessi") ("ex") ("àteru") ;

LIST Mese = "ghennàrgiu" "ennalzu" "freàrgiu" "martzu" "abrile" "maju" "làmpadas" "trìulas" "argiolas" "austu" "cabudanni" "santugaine" "santandria" "nadale" ;
SET Dignidade = ("presidente") OR ("guvernadore") OR ("imperadore") OR ("tzar") OR ("re") OR ("giuighe") OR ("giuighissa") OR ("prìntzipe") OR ("barone") OR ("conte") OR ("visconte") OR ("marchesu") OR ("duca") OR ("archiduca") OR ("pìscamu") OR ("bìscamu") OR ("artzipìscamu") OR ("artzibìscamu") OR ("califa") OR ("emiru") OR ("sultanu") OR ("khan") OR ("satrapa") ;


# Barriers

# SET S-BOUNDARY = CS | Interr | EOS;

SET Barrera_Final_SN = EOS OR Prep OR Det OR CnjSub OR CnjAdv OR Vall OR Rel OR Pro ;	# Vall és perillós per la ambigüitat, però en general, prolífic
SET Barrera_Final_Estrict_SN = EOS OR Prep OR Det OR CnjSub OR CnjAdv OR Rel OR Pro ;
SET Barrera_Inicial_SN = BOS OR Prep OR CnjSub OR CnjAdv OR Vall OR Rel ;		# Vall és perillós per la ambigüitat, però en general, prolífic
SET Barrera_Inicial_Estrict_SN = BOS OR Prep OR CnjSub OR CnjAdv OR Rel ;
SET Barrera_Inicial_Prop = BOS OR CnjSub OR CnjAdv OR Rel ;
SET Limite_Barrera_Inicial_SN = ("èssere") OR ("parre") OR ("pàrrere") OR ("àere") ;

SECTION

REMOVE Imper IF (NOT 1* Excl) ;
SELECT Imper IF (0C Vall) (1* Excl) (NOT -1* Vall + Pers) (NOT 1* Vall + Pers) ;

SELECT Pref IF (0 (META:/-/r)) (1 N OR A OR V OR unknown) ;
SELECT Pref IF (1 ("-")) (2 N OR A OR V OR unknown) ;
REMOVE Pref ;	# IF (0 ("anti") OR ("auto") OR ("multi") OR ("neo") OR ("pluri") OR ("post") OR ("pre") OR ("proto") OR ("ex") OR ("semi")) (NOT 0 (META:/-/r)) (NOT 1 ("-")) ;

REMOVE Vlex IF (0 Vmod) (1 Vall + Inf) ;
REMOVE Vlex IF (0 Vmod) (1 Adv OR Pron) (2 Vall + Inf) ;
REMOVE Vmod IF (NOT 1 Inf) (NOT 2 Inf) ;
SELECT Inf IF (-1C Vmod) ;
SELECT Inf IF (-2C Vmod) (-1 Adv OR Pron) ;
SELECT Inf IF (-1C Vall) (0 Inf) ;
SELECT Inf IF (-2C Vall) (-1 Adv OR Pron) (0 Inf) ;
REMOVE Vconj IF (-1C Prep) ;
REMOVE Vconj IF (-2C Prep) (-1 Pro) (0 Inf) ;
REMOVE Vconj IF (-3C Prep) (-2 Pro) (-1 Pro) (0 Inf) ;
REMOVE Subj IF (NOT -1* CnjSub OR CnjAdv) (NOT 0 Ser OR Istare) ;	# Cantare
REMOVE Subj IF (NOT -1* Vall + Pers) (NOT 1* Vall + Pers) (NOT 0 Ser OR Istare) ;	# Cantare
REMOVE Subj IF (-2 Ser OR Haver) (-1 PP) ;	# an apiu atentzione
REMOVE Subj IF (-3 Ser OR Haver) (-2 Adv) (-1 PP) ;
REMOVE Subj IF (-3 Ser OR Haver) (-2 PP) (-1 Adv) ;
REMOVE Subj IF (-1 ("pro") OR De) (0 Inf) ;
REMOVE Subj IF (NOT -4 CnjSub OR CnjAdv) (NOT -3 CnjSub OR CnjAdv) (NOT -2 CnjSub OR CnjAdv) (NOT -1 CnjSub OR CnjAdv) (0 Intre) ;

REMOVE ("èssere") IF (-1 Det + Def) (NOT -1 MajIni) (0 Est) (0 MajIni) ;
SELECT N IF (-2 Prep) (-1 Det + Def) (0 Est) (0 MajIni) ;	# cara a l'Est
SELECT N IF (-2 ("a") OR ("cara a") OR ("de")) (-1 Det + Def) (0 Est) ;	# a l'est
SELECT N IF (-2 Ovest) (-1 Prep) (0 Est) ;			# d'Ouest en Est
SELECT N IF (0 Ovest) (1 Prep) (1 Est) ;			# d'Ouest en Est
SELECT N IF (-2 Est) (-1 Prep) (0 Ovest) ;			# d'Est en Est
SELECT N IF (0 Est) (1 Prep) (2 Ovest) ;			# d'Est en Est
REMOVE Vall IF (-1 Ovest) (-1 (META:/-/r)) (0 Est) ;
REMOVE Vall IF (0 Est) (0 (META:/-/r)) (1 Ovest) ;
SELECT A IF (-1 Top) (0 Est) (0 MajIni) ;			# Jérusalem Est
REMOVE Vall IF (-1 ("nord") OR ("sud") OR ("ovest") OR ("tzentru")) (-1 (META:/-/r)) (0 Est) ;
REMOVE Vall IF (-1 ("nord") OR ("sud") OR ("ovest")) (0 Est) (NOT 1 PP OR A) ;
REMOVE Vall IF (-1 ("estremu")) (0 Est) (1 De) ;
REMOVE Vall IF (-2 Lpar) (-1 ("estremu")) (0 Est) ;
REMOVE Vall IF (-3 ("a") OR ("in") OR ("subra") OR ("susu") OR ("a curtzu de") OR ("cara a")) (-2 Det + Def) (-1 ("estremu")) (0 Est) ;
REMOVE Vall IF (-1 N + MinIni) (0 ("<Est>")) (1 EOS OR MinIni) ;	# la porte Est

REMOVE N IF (0 Est) (-1 Interr OR Rel) ;
REMOVE A IF (0 Est) (-1 Interr OR Rel) ;	# Où est Jean ?
SELECT Ser IF (0 Est) (1 Adv) ;
SELECT Ser IF (0 Est) (1 ("in")) (2 N); ## est en conséquence
SELECT Ser IF (0 Est) (1 A) ;
SELECT Ser IF (0 Est) (1 PP) ;
SELECT Ser IF (0 Est) (1 Det) ;
SELECT Ser IF (0 Est) (1 NP) ;
SELECT Ser IF (0 Est) (1 unknown) ;
SELECT Ser IF (NOT -1 DetDef OR ("lìmite") OR ("frontera") OR ("cara") OR ("fatzada") OR ("costa") OR ("costera") OR ("ala") OR ("costàgiu")) (0 Est) (NOT 0 MajIni) ;

SELECT Fem IF (NOT -1/* M) (0 ("biennale") + Masc) (NOT 1 M) ;

LIST SuChi = "<su chi>"i "<sa chi>"i "<sos chi>"i "<sas chi>"i ;
REMOVE Rel IF (-1 Prep) (0 SuChi) ;
REMOVE Rel IF (-1 ("totu")) (0 SuChi) ;
REMOVE Pron IF (0 SuChi) ;

REMOVE ("medu") + A IF (NOT -1 Noun_Adj) ;
REMOVE ("medu") + A IF (NOT -1 Noun_Adj + FemSg) (0 ("<meda>"i)) ;
REMOVE ("medu") + A IF (NOT -1 Noun_Adj + FemPl) (0 ("<medas>"i)) ;
REMOVE ("medu") + A IF (-1 Noun_Adj + &&ConcordaGN) (0 ("meda") + A + &&ConcordaGN) ;	# guess
REMOVE ("medu") + A IF (-1 Noun_Adj + &&ConcordaGN) (0 ("meda_post") + &&ConcordaGN) ;	# guess
REMOVE ("medu") + N + Fem ;	# guess
SELECT A IF (-2C Barrera_Inicial_Estrict_SN) (-1C N + Pl) (0 ("meda")) ;	# in paisos medas
SELECT A IF (-3C Barrera_Inicial_Estrict_SN) (-2 N + Pl + &&ConcordaGN) (-1 A + Pl + &&ConcordaGN) (0 ("meda")) ;	# in paisos europeos medas
SELECT A IF (-1 ("<bias>"i) OR ("<bortas>"i)) (0 ("<medas>"i)) ;
SELECT N IF (0 ("<bias>"i) OR ("<bortas>"i)) (1 ("<medas>"i)) ;
SELECT N IF (-1 ("<medas>"i)) (0 ("<bias>"i) OR ("<bortas>"i)) ;
SELECT A IF (0 ("<medas>"i)) (1 ("<bias>"i) OR ("<bortas>"i)) ;
REMOVE N IF (NOT -1/* Det + Def + MascPl) (0 ("meda")) ;

SELECT Prep IF (0 ("in logu")) (1 A + MascSg) ;	# in logu suo
REMOVE Prep IF (0 ("in logu")) ;

SELECT Adv IF (-1C V) (0 ("<meda>"i)) (NOT -1 N + Sg) ;	# mudat meda ; est mudende meda
SELECT Adv IF (-2 Haver) (-1C PP) (0 ("<meda>"i)) (NOT -1 N + Sg) ;	# at mudadu meda
SELECT Adv IF (-3 Haver) (-2 Adv) (-1C PP) (0 ("<meda>"i)) (NOT -1 N + Sg) ;

REMOVE Cog IF (0 ("<XI>")) ;

SELECT ("peso") IF (-1 Num) (0 ("pesu")) ;
SELECT ("peso") IF (0 ("pesu")) (1 ("argentinu") OR ("bolivianu") OR ("colombianu") OR ("cubanu") OR ("dominicanu") OR ("filipinu") OR ("honduregnu") OR ("messicanu") OR ("tzilenu") OR ("uruguaianu")) ;
REMOVE ("peso") IF (-2 ("mesura")) ;	# mesuras e pesos
REMOVE ("peso") IF (2 ("mesura")) ;	# pesos e mesuras
REMOVE ("pesu") IF (0 ("peso")) ;

REMOVE Aord IF (0 ("<de un.>"ir) + Aord) ;
SELECT Aord IF (-1/* Det + Def) (0 ("<de .*>"r) + Aord) ;
REMOVE Aord IF (-1 Noun_Adj) (0 ("<de .*>"r) + Aord) ;
REMOVE Aord IF (0 ("<de .*>"r) + Aord) ;

SELECT CnjAdv IF (0 Comente) (1C Prep) ;	# comente in cale si siat àtera limba
SELECT CnjAdv IF (-1 ("gai")) (0 Comente) (1 Det) ;
SELECT CnjAdv IF (-1 ("gai")) (0 Comente) (1 NP) ;	# gai comente Navarra
SELECT Prep IF (0 Comente) (1C N_A) ;

REMOVE CnjAdv OR CnjSub IF (0 Prep) (1C Inf) ;	# pro ischire
REMOVE CnjAdv OR CnjSub IF (0 Prep) (1 Pro) (2C Inf) ;
REMOVE CnjAdv OR CnjSub IF (0 Prep) (1 Pro) (2 Pro) (3C Inf) ;
REMOVE CnjAdv OR CnjSub IF (0 ("si")) (1C Inf) ;	# si ischire
REMOVE CnjAdv OR CnjSub IF (0 ("si")) (1 Pro) (2C Inf) ;
REMOVE CnjAdv OR CnjSub IF (0 ("si")) (1 Pro) (2 Pro) (3C Inf) ;
SELECT Prep IF (0 ("pro")) (1 Inf) ;	# pro produire
SELECT Prep IF (0 ("pro")) (1 Pro) (2 Inf) ;
SELECT Prep IF (0 ("pro")) (1 Pro) (2 Pro) (3 Inf) ;
SELECT Prep IF (0 ("pro")) (NOT 1* Vall + Subj) (NOT 1C Vall + Pers) ;

REMOVE ("candire") IF (0 ("cando")) ;	# GUESS

SELECT ("argiolas") IF (-1 Num) (0 ("argiolas")) ;	# 13 argiolas
SELECT ("argiolas") IF (0 ("argiolas")) (1 Num) ;	# argiolas 2023

SELECT N IF (-1 Det + Def + MascSg) (0 ("prus")) (1 De) ;
REMOVE N IF (0 ("prus")) ;

REMOVE Adv IF (0 ("in parte")) (1 De) ;
REMOVE Adv IF (0 ("in parte")) (1 Pos + FemSg) ;
SELECT Adv IF (0 ("in parte")) ;

SELECT CnjAdv IF (0 Si) (1 Si) (1 Pro OR Vall) ;
REMOVE CnjAdv IF (0 Si) (1C Inf OR Ger) ;	# de se crear
REMOVE CnjAdv IF (0 Si) (1 Pro) (2C Inf OR Ger) ;	# de se lo crear
REMOVE Pron IF (0 Si) (NOT 1 Pro OR Vall + P3 OR Vall + Inf OR Vall + Ger OR unknownMinIni) ;	# si dao
REMOVE Pron IF (0 Si) (1 Pro LINK NOT 0 Ddu) (NOT 2 Pro OR Vall + P3 OR Vall + Inf OR Vall + Ger OR unknownMinIni) ;	# si lu dao ; si ddu dao
REMOVE Pron IF (0 Si) (1 Pro) (2 Pro) (NOT 3 Pro OR Vall + P3 OR Vall + Inf OR Vall + Ger OR unknownMinIni) ;	# si bi lu dao
REMOVE Pron IF (0 Si) (1 Pro) (2 Pro) (3 Pro) (4 Vall + P3 OR Vall + Inf OR Vall + Ger OR unknownMinIni) ;
SELECT CnjAdv IF (NOT -1C* Rel OR CnjSub OR CnjAdv) (0 Si) (1C Vall + Pers) (2C Vall + Pers) ;	# Se tot aquò se met en plaça serà
SELECT Pro IF (0 Si) (1 unknownMinIni) ;
SELECT Pro IF (0 Si) (1 Pro) (2 unknownMinIni) ;
SELECT Pro IF (0 Si) (1 Pro) (2 Pro) (3 unknownMinIni) ;
SELECT Pro IF (0 Si) (1 Pro) (2 Pro) (3 Pro) (4 unknownMinIni) ;
REMOVE Pron IF (-1C Vall LINK NOT 0 PP) (0 Si) ;
REMOVE Pron IF (-2C Vall LINK NOT 0 PP) (-1C Adv OR PreAdv OR ("pas") LINK NOT 0 Interr OR Rel) (0 Si) ;
REMOVE Pron IF (-3C Vall LINK NOT 0 PP) (-2C Adv OR PreAdv OR ("pas") LINK NOT 0 Interr OR Rel) (-1 Adv LINK NOT 0 Interr) (0 Si) ;
REMOVE Pron IF (-2 Haver) (-1C PP) (0 Si) ;
REMOVE Pron IF (-3 Haver) (-2C Adv OR PreAdv LINK NOT 0 Interr OR Rel) (-1C PP) (0 Si) ;
REMOVE Pron IF (-4 Haver) (-3C Adv OR PreAdv LINK NOT 0 Interr OR Rel) (-2 Adv LINK NOT 0 Interr OR Rel) (-1C PP) (0 Si) ;
SELECT Pro IF (0 Si) (1 Nde) (2 ("andare")) ;
SELECT Pro IF (0 Si) (1 Nde) (2 Si) (3 ("andare") + PP) ;
SELECT Pro IF (0 Si) (1 Nde) (2 Si) (3 Adv) (4 ("andare") + PP) ;
SELECT Pro IF (0 Si) (1 Nde) (2 Si) (3 Adv) (4 Adv) (5 ("andare") + PP) ;
SELECT Pro IF (-1C Rel) (0 Si) (1 Pro OR Vall + P3) ;
SELECT Pro IF (-2C Rel) (-1 Adv) (0 Si) (1 Pro OR Vall + P3) ;
SELECT Pro IF (-3C Rel) (-2 Adv) (-1 Adv) (0 Si) (1 Pro OR Vall + P3) ;
#REMOVE Pron IF (0 Si) (1 ("ne")) ;	# Se n’èram estadas amigas
SELECT CnjAdv IF (0 Si) (1C Ser) (2C N_A) ;	# s'es positiu
SELECT CnjAdv IF (0 Si) (1C Ser) (2 Adv) (3C N_A) ;	# s'es pas positiu
SELECT CnjAdv IF (0 Si) (1C Ser) (2 Adv) (3 Adv) (4C N_A) ;	# s'es pas totjorn positiu
SELECT CnjAdv IF (0 Si) (1 Pro) (2 Pro) (3 V) ;
SELECT Pro IF (-1C Tn) (-1 Tn + &&ConcordaSP) (NOT -1 Pers) (0 Si) (1 Pro OR Vall + P3 + &&ConcordaSP) ;	# medas si ghetaiant
SELECT Pro IF (-1C Tn) (-1 Tn + &&ConcordaSP + P3) (0 Si) (1 Pro OR Vall + P3 + &&ConcordaSP) ;

SELECT Det IF (0 ("unu")) (1 unknown) ;
SELECT Det IF (0 ("su")) (1 unknown) ;
SELECT Det IF (0 Det + Dem) (1 unknown) ;

SELECT Det IF (0 ("su")) (1 De) (2 Num) ;	# su de tres

SELECT ("làmpadas") IF (-1 ("[1-9][0-9]?"r)) (0 ("làmpadas")) ;
SELECT ("làmpadas") IF (-2 Num) (-1 De) (0 ("làmpadas")) ;
SELECT ("làmpadas") IF (-2 ("mese")) (-1 De) (0 ("làmpadas")) ;
SELECT ("làmpadas") IF (-2 Mese) (-1 CM OR CnjCoo) (0 ("làmpadas")) ;
SELECT ("làmpadas") IF (0 ("làmpadas")) (1 CM OR CnjCoo) (2 Mese) ;

SELECT N IF (-1 Det + &&ConcordaGN) (0 ("partidu") + &&ConcordaGN) ;	# custu partidu

SELECT N IF (-1 Det + Masc) (0 ("de s'aneddu")) ;	# su de s'aneddu
REMOVE N IF (0 ("de s'aneddu")) ;

SELECT ("insinnare") IF (0 ("insegnare")) ;	# dcorraine

REMOVE ("mudire") IF (0 ("mudare")) ;	# guess

REMOVE PreDet IF (NOT 1 Det) ;	# faghent totu in italian
REMOVE Adv IF (0 InTotu) (1 Det + &&ConcordaGN) (2 N_A + &&ConcordaGN) ;	# in totu s'istadu
REMOVE SUB:1 PreDet IF (0 InTotu) (NOT 1 Det) ;
REMOVE Adv IF (-1 ("totu")) (0 ("custu")) ;

SELECT PreDet + &&ConcordaGN IF (0 PreDet + &&ConcordaGN) (1 Det + Def + &&ConcordaGN) (2 N + &&ConcordaGN) ;	# ambedue le aree politiche
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + Ind + &&ConcordaGN) (1 N + &&ConcordaGN) ;	# unu pitzocu sardu ; sos àteros puntos
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + &&ConcordaGN) (1C N_A + &&ConcordaGN) ;
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + &&ConcordaGN) (1 N_A + &&ConcordaGN) (2 N_A + &&ConcordaGN) ;	# unu pitzocu sardu
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + &&ConcordaGN) (1 Adv OR PreAdv) (2 A + &&ConcordaGN) ;	# sa prus antiga
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + &&ConcordaGN) (1 Num) (2 N_A + &&ConcordaGN + Pl) ;	# unas 150 limbas
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + Ind + &&ConcordaGN) (1 Pref) (1 N + &&ConcordaGN) ;	# unu pitzocu sardu ; sos àteros puntos
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + &&ConcordaGN) (1 Pref) (2C N_A + &&ConcordaGN) ;
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + &&ConcordaGN) (1 Pref) (2 N_A + &&ConcordaGN) (3 N_A + &&ConcordaGN) ;
SELECT Det + &&ConcordaGN IF (NOT -1/* Det) (0 Det + &&ConcordaGN) (1 Num) (2 Pref) (3 N_A + &&ConcordaGN + Pl) ;

LIST AdvLogu = "in dae in antis" "a dae in antis" "in antis" "in dae segus" "in segus" "a segus" "dae segus" "segus" "a curtzu" ;
SELECT A + Pos IF (-2/* Det + &&ConcordaGN) (-1 N_A + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;	# sas domos meas
SELECT A + Pos IF (-2/* Det + &&ConcordaGN) (-1 N_A + unknown) (0 A + Pos + &&ConcordaGN) ;
SELECT A + Pos IF (-3/* Det + &&ConcordaGN) (-2 N_A + &&ConcordaGN) (-1 N_A + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;
SELECT A + Pos IF (-4/* Det + &&ConcordaGN) (-3 N_A + &&ConcordaGN) (-2 N_A + &&ConcordaGN) (-1 N_A + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;	# sas minorias linguisticas internas suas
SELECT A + Pos IF (-3/* Det + Def) (-2 De) (-1 Num) (0 A + Pos) ;	# su de 10 suo
SELECT A + Pos IF (-4/* Det + Def) (-3 De) (-2 Num) (-1 N) (0 A + Pos) ;	# su de 10 anniversarios suo
SELECT A + Pos IF (-5/* Det + Def) (-4 De) (-3 Num) (-2 N_A + &&ConcordaGN) (-1 N_A + &&ConcordaGN) (0 A + Pos) ;
SELECT A + Pos IF (-6/* Det + Def) (-5 De) (-4 Num) (-3 N_A + &&ConcordaGN) (-2 N_A + &&ConcordaGN) (-1 N_A + &&ConcordaGN) (0 A + Pos) ;
SELECT A + Pos IF (-3 N_A + &&ConcordaGN) (-2 De) (-1 N) (0 A + Pos + &&ConcordaGN) ;	# su maistru de sala suo
SELECT Pron + Pos IF (-1 AdvLogu) (0 Pron + Pos) ;	# in antis suo
SELECT Adv IF (0 AdvLogu) (0 Pron + Pos) ;	# in antis suo
SELECT A + Pos IF (-1/* Det + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;	# a su suo
SELECT Pron + Pos IF (-1 Ser + &&ConcordaSP) (0 A + Pos + &&ConcordaSP) ;	# est suo
SELECT A + Pos IF (-2 Ser + &&ConcordaSP) (-1 N + &&ConcordaSP) (0 A + Pos + &&ConcordaSP) ;	# est amigu suo
SELECT A + Pos IF (-2 ("pro")) (-1/* N + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;	# pro parte sua
SELECT A + Pos IF (-2 ("in")) (-1/* N + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;
SELECT A + Pos IF (-2 ("a")) (-1/* N + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;	# a pàrrere suo
SELECT A + Pos IF (-2 Prep) (-1/* N + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;	# cun sorre sua
SELECT A + Pos IF (-2 ("a")) (-1/* ("pàrrere") + MascSg) (0 A + Pos + MascSg) ;	# a pàrrere meu
REMOVE Pron + Pos IF (-1 N_A + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;	# est amigu suo
SELECT A + Pos IF (-1 unknown) (0 Suo + A) ;
SELECT A + Pos IF (-2 unknown) (-1 A + &&ConcordaGN) (0 Suo + A + &&ConcordaGN) ;
SELECT A + Pos IF (-2 PP + &&ConcordaGN) (-1 A + &&ConcordaGN) (0 Suo + A + &&ConcordaGN) ;	# unknown noun
SELECT A + Pos IF (-1 Ant + &&ConcordaGN) (0 A + Pos + &&ConcordaGN) ;	# Pàule meu ; Pàule suo
SELECT Pron + Pos IF (-1C Prep) (0 Suo) ;

SELECT Ref IF (0 Si) (1C V + Sg3) ;

SELECT Interr IF (0 Ite OR ProIte OR Cale OR Cantu OR Comente OR Ue) (1* ("<?>")) ;

LIST V_Interr = "ischire" "sàpere" "pedire" "petire" "pregontare" "dimandare" "nàrrere" "ispiegare" ;
SELECT Interr IF (-1 V_Interr) (0 Ite OR ProIte OR Cale OR Cantu OR Comente OR Ue) ;
SELECT Interr IF (-2 V_Interr) (-1 Adv) (0 Ite OR ProIte OR Cale OR Cantu OR Comente OR Ue) ;
SELECT Interr IF (0 Ite OR ProIte OR Cale OR Cantu OR Ue) (1 ("a")) (2 Inf) ;
SELECT Interr IF (0 Ite OR ProIte OR Cale OR Cantu OR Ue) (1 ("a")) (2 Pro) (3 Inf) ;
SELECT Interr IF (0 Ite OR ProIte OR Cale OR Cantu OR Ue) (1 ("a")) (2 Pro) (3 Pro) (4 Inf) ;

SELECT Rel IF (-1 Det + Def) (0 Ite) ;
SELECT PreAdv IF (0 Ite) (1 A) (2 Chi) ;

REMOVE Imper IF (0 Dare + Prs) (1 Inf) ;

REMOVE Interj IF (-1 Det) ;
SELECT Interj IF (NOT -1/* Det + FemPl) (0 ("bonas dies")) ;
SELECT Interj IF (NOT -1/* Det + FemPl) (0 ("dies bonas")) ;
SELECT Interj IF (NOT -1/* Det + FemSg) (0 ("bona die")) ;
SELECT Interj IF (NOT -1/* Det + FemPl) (0 ("bonas tardas")) ;
SELECT Interj IF (NOT -1/* Det + FemPl) (0 ("tardas bonas")) ;
SELECT Interj IF (NOT -1/* Det + FemSg) (0 ("bona note")) ;
SELECT Interj IF (NOT -1/* Det + FemSg) (0 ("note bona")) ;
REMOVE Interj IF (NOT 1* ("!")) ;
SELECT Interj IF (-1 BOS OR CM) (1 ("!")) ;
SELECT Interj IF (1 CM) (1* ("!")) ;

LIST Adj_Sentimentu = "cuntentu" ;	# Adj = PP
SELECT Ser IF (-1 Ser + &&ConcordaSP) (0 Ser + PP + &&ConcordaSP + &&ConcordaGN) (1 Adj_Sentimentu + &&ConcordaGN) ;	# es istado cuntentu
SELECT Ser IF (-1 Ser + &&ConcordaSP) (0 Ser + PP + &&ConcordaSP + &&ConcordaGN) (1 PreAdv OR Adv) (2 Adj_Sentimentu + &&ConcordaGN) ;
SELECT Ser IF (-2 Ser + &&ConcordaSP) (-1 Adv) (0 Ser + PP + &&ConcordaSP + &&ConcordaGN) (1 Adj_Sentimentu + &&ConcordaGN) ;	# es semper istado cuntentu
SELECT A IF (-2 Ser + &&ConcordaSP) (-1 Ser + PP + &&ConcordaSP + &&ConcordaGN) (0 Adj_Sentimentu + &&ConcordaGN) ;	# es istado cuntentu
SELECT A IF (-3 Ser + &&ConcordaSP) (-2 Ser + PP + &&ConcordaSP + &&ConcordaGN) (0 PreAdv OR Adv) (2 Adj_Sentimentu + &&ConcordaGN) ;
SELECT A IF (-3 Ser + &&ConcordaSP) (-2 Adv) (-1 Ser + PP + &&ConcordaSP + &&ConcordaGN) (0 Adj_Sentimentu + &&ConcordaGN) ;	# es semper istado cuntentu

SELECT Istare IF (-1 Ser + &&ConcordaSP) (0 Ser + PP + &&ConcordaSP + &&ConcordaGN) (1 PP + &&ConcordaGN) ;	# es istada faeddada
SELECT Istare IF (-1 Ser + &&ConcordaSP) (0 Ser + PP + &&ConcordaSP + &&ConcordaGN) (1 Adv) (2 PP + &&ConcordaGN) ;	# es istada semper faeddada
SELECT Istare IF (-2 Ser + &&ConcordaSP) (-1 Adv) (0 Ser + PP + &&ConcordaSP + &&ConcordaGN) (1 PP + &&ConcordaGN) ;	# es semper istada faeddada
SELECT Istare IF (-1 Ser + &&ConcordaSP) (0 Ser + PP + &&ConcordaSP + &&ConcordaGN) (1 Adv) (2 Adv) (3 PP + &&ConcordaGN) ;
SELECT PP IF (-2 Ser + &&ConcordaSP) (-1 Istare + PP + &&ConcordaSP + &&ConcordaGN) (0 PP + &&ConcordaGN) ;	# es istada faeddada
SELECT PP IF (-3 Ser + &&ConcordaSP) (-2 Istare + PP + &&ConcordaSP + &&ConcordaGN) (-1 Adv) (0 PP + &&ConcordaGN) ;	# es istada semper faeddada
SELECT PP IF (-3 Ser + &&ConcordaSP) (-2 Adv) (-1 Istare + PP + &&ConcordaSP + &&ConcordaGN) (0 PP + &&ConcordaGN) ;	# es semper istada faeddada
SELECT PP IF (-4 Ser + &&ConcordaSP) (-3 Istare + PP + &&ConcordaSP + &&ConcordaGN) (-2 Adv) (-1 Adv) (0 PP + &&ConcordaGN) ;
SELECT Ser IF (-1 Ser + &&ConcordaSP) (0 Ser + PP + &&ConcordaSP) ;

LIST Vintr = "andare" "bènnere" "arribbare" "torrare" "artziare" "pèndere" "intrare" "essire" "nàschere" "mòrrere" ;
SELECT PP IF (-1 Ser + &&ConcordaSP) (0 Vintr + &&ConcordaSP) ;
SELECT PP IF (-2 Ser + &&ConcordaSP) (-1 Adv) (0 Vintr + &&ConcordaSP) ;

REMOVE Acr IF (0 ("<as>")) ;
REMOVE Aere OR ("amu") IF (0 Haver) (1 PP) ;
REMOVE Aere OR ("amu") IF (0 Haver) (1 Adv) (2 PP) ;
REMOVE Aere OR ("amu") IF (0 Haver) (1 Adv) (2 Adv) (3 PP) ;
REMOVE Aere OR ("amu") IF (0 Haver) (1 ("a")) (2 Inf) ;	# at a èssere
REMOVE Aere OR ("amu") IF (0 Haver) (1 Adv) (2 ("a")) (3 Inf) ;
SELECT Inf (-2 Haver) (-1 ("a")) (0 Inf) ;	# at a èssere
SELECT Inf (-3 Haver) (-2 Adv) (-1 ("a")) (0 Inf) ;
REMOVE Haver ;

SELECT PP IF (-1 Haver) ;
SELECT PP IF (-2 Haver) (-1 Adv) ;
SELECT PP IF (-3 Haver) (-2 Adv) (-1 Adv) ;
SELECT PP IF (-2 Ref) (-1 Ser) ;
SELECT PP IF (-3 Ref) (-2 Ser) (-1 Adv) ;
SELECT PP IF (-4 Ref) (-3 Ser) (-2 Adv) (-1 Adv) ;
SELECT PP IF (-3 Ref) (-2 Pro) (-1 Ser) ;
SELECT PP IF (-4 Ref) (-3 Pro) (-2 Ser) (-1 Adv) ;
SELECT PP IF (-5 Ref) (-4 Pro) (-3 Ser) (-2 Adv) (-1 Adv) ;


SELECT Adv IF (-1C A) (0 Meda) ;	# est bianca meda
SELECT A IF (NOT -1 N) (0 A) (1 Meda) ;	# est bianca meda
SELECT Adv IF (-1C A) (0 Meda) ;	# est bianca meda
SELECT Adv IF (-3/* Det + &&ConcordaGN) (-2 N + &&ConcordaGN) (-1 A + &&ConcordaGN) (0 Meda) ;	# una proposta sèria meda
SELECT Adv IF (-4/* Det + &&ConcordaGN) (-3 N + &&ConcordaGN) (-2 A + &&ConcordaGN) (-1 A + &&ConcordaGN) (0 Meda) ;
SELECT Adv IF (-4/* Det + &&ConcordaGN) (-3 A + &&ConcordaGN) (-2 N + &&ConcordaGN) (-1 A + &&ConcordaGN) (0 Meda) ;
SELECT A IF (-1C N_A) (0 Meda) (1C EOS OR Prep OR Det OR CnjAdv OR Rel OR Pro OR Vall) ;	# chi dae annos meda sos Maputzes sunt
SELECT A IF (-1C N_A) (0 Meda) (1 Adv) (2C Vall) ;	# discussiones meda non servint

# Rel
SELECT CnjSub IF (-2 Ser + P3 + Sg) (-1 A + Sg) (0 Chi) ;	# Est craru chi
SELECT CnjSub IF (-3 Ser + P3 + Sg) (-2 Adv) (-1 A + Sg) (0 Chi) ;
SELECT CnjSub IF (-3 Ser + P3 + Sg) (-2 A + Sg) (-1 Meda) (0 Chi) ;	# Est craru meda chi
SELECT CnjSub IF (-1C Vconj) (0 Chi) ;
SELECT CnjSub IF (-2C Vconj) (-1 Adv) (0 Chi) ;
SELECT CnjSub IF (-3C Vconj) (-2 Adv) (-1 Adv) (0 Chi) ;
SELECT Rel IF (-1C N_A) (0 Chi) ;

# Det
SELECT Det IF (0 Det + Def + Masc + &&ConcordaSP) (1 Det + Def + Fem + &&ConcordaSP) (2 N_A + &&ConcordaSP) ;	# su/sa diretore/a
SELECT Det IF (0 Det + Def + Fem + &&ConcordaSP) (1 Det + Def + Masc + &&ConcordaSP) (2 N_A + &&ConcordaSP) ;	# sa/su diretore/a
SELECT Det IF (-1 Det + Def + Masc + &&ConcordaSP) (0 Det + Def + Fem + &&ConcordaSP) (1 N_A + &&ConcordaSP) ;	# su/sa diretore/a
SELECT Det IF (-1 Det + Def + Fem + &&ConcordaSP) (0 Det + Def + Masc + &&ConcordaSP) (1 N_A + &&ConcordaSP) ;	# sa/su diretore/a
SELECT Det IF (0 Det + &&ConcordaSP) (0 (META:/[\/]/r)) (1 ("<a>") OR ("<os>") OR ("<as>") OR ("<s>")) (2 N_A + &&ConcordaSP) ;	# su/a diretore/a
SELECT Det IF (-1 Prep) (0 Det + Def + Masc + &&ConcordaSP) (0 (META:/[\/]/r)) (1 Prep) (2 Det + Def + Fem + &&ConcordaSP) (3 N_A + &&ConcordaSP) ;	# dae su/dae sa diretore/a
SELECT Det IF (-1 Prep) (0 Det + Def + Fem + &&ConcordaSP) (0 (META:/[\/]/r)) (1 Prep) (2 Det + Def + Masc + &&ConcordaSP) (3 N_A + &&ConcordaSP) ;	# dae sa/dae su diretore/a
SELECT Det IF (-3 Prep) (-2 Det + Def + Masc + &&ConcordaSP) (-2 (META:/[\/]/r)) (-1 Prep) (0 Det + Def + Fem + &&ConcordaSP) (1 N_A + &&ConcordaSP) ;	# dae su/dae sa diretore/a
SELECT Det IF (-3 Prep) (-2 Det + Def + Fem + &&ConcordaSP) (-2 (META:/[\/]/r)) (-1 Prep) (0 Det + Def + Masc + &&ConcordaSP) (1 N_A + &&ConcordaSP) ;	# dae sa/dae su diretore/a
SELECT Det + &&ConcordaGN IF (0 Det + &&ConcordaSP) (1 Adv) (2 Num) (3 N_A + &&ConcordaGN)	;	# sas belle 150 limbas
SELECT Det IF (0 Det + Def + MascSg) (1 De) (2 Inf) ;
SELECT Det IF (0 Det + Def + MascSg) (1 De) (2 Pro) (3 Inf) ;
SELECT Det IF (0 Det + Def + MascSg) (1 De) (2 Pro) (3 Pro) (4 Inf) ;
REMOVE Det IF (NOT 1 N OR NP OR A OR PP OR Adv OR Num OR unknown) ;
REMOVE Det IF (1C Adv) (NOT 2 A OR PP) ;
REMOVE Pron IF (0 Det + Def + &&ConcordaGN) (1C N_A + &&ConcordaGN) ;
REMOVE Pron IF (0 Det + Def + &&ConcordaGN) (1C NP + &&ConcordaGN) ;	# su Gia islàmicu
SELECT Det (0 Det + Def + MascSg) (1C Num) ;	# su 3
SELECT Det (-1 BOS) (0 Det + Def + &&ConcordaSP) (1 N + &&ConcordaSP) ;	# sas lissèntzias
SELECT Det (-1 BOS) (0 Det + Def + &&ConcordaSP) (1 A + &&ConcordaSP) (2 N + &&ConcordaSP) ;

SELECT Num IF (-1 Det + Def + Fem + Sg) (0 ("<una>"i)) ;	# sa una de note

# Adv
REMOVE PreAdv IF (NOT 1 A OR PP OR Adv) ;
REMOVE PreAdv IF (-1 Haver) (1 PP) ;
SELECT Adv IF (-1 Ser OR Cunsiderare OR Giudicare OR Parrere OR Resurtare) (0 Megius OR Peus) (1 ("a")) (2 Inf) ;	# est mègius a abbandonare
SELECT Adv IF (-1 Ser OR Cunsiderare OR Giudicare OR Parrere OR Resurtare) (0 Megius OR Peus) (1 ("a")) (2 Pro) (3 Inf) ;	# est mègius a l’abbandonare
SELECT Adv IF (-1 Ser OR Cunsiderare OR Giudicare OR Parrere OR Resurtare) (0 Megius OR Peus) (1 ("a")) (2 Pro) (3 Pro) (4 Inf) ;
SELECT Adv IF (-2 Ser OR Cunsiderare OR Giudicare OR Parrere OR Resurtare) (-1 Adv) (0 Megius OR Peus) (1 ("a")) (2 Inf) ;
SELECT Adv IF (-2 Ser OR Cunsiderare OR Giudicare OR Parrere OR Resurtare) (-1 Adv) (0 Megius OR Peus) (1 ("a")) (2 Pro) (3 Inf) ;
SELECT Adv IF (-2 Ser OR Cunsiderare OR Giudicare OR Parrere OR Resurtare) (-1 Adv) (0 Megius OR Peus) (1 ("a")) (2 Pro) (3 Pro) (4 Inf) ;
SELECT PreAdv IF (1C A OR PP OR Adv) ;	# est belle inesistente


# Pron
SELECT Pron IF (0 Custu + &&ConcordaSP) (1 Vall + &&ConcordaSP) (NOT 0 Custu + &&ConcordaGN LINK 1 N_A + &&ConcordaGN) ;
SELECT Pron IF (0 Custu + &&ConcordaSP) (1 Adv) (2 Vall + &&ConcordaSP) (NOT 0 Custu + &&ConcordaGN LINK 1 N_A + &&ConcordaGN) ;
SELECT Pron IF (0 Custu + &&ConcordaSP) (1 Adv) (2 Adv) (3 Vall + &&ConcordaSP) (NOT 0 Custu + &&ConcordaGN LINK 1 N_A + &&ConcordaGN) ;

LIST Verbos_CI = "agradare" "disagradare" "dòlere" "geniare" "pràghere" ;
REMOVE Lu IF (0 Li) (1 Verbos_CI) ;
REMOVE Lu IF (0 Li) (1 Haver OR Vmod) (2 Verbos_CI) ;
REMOVE Lu IF (0 Li) (1 Haver) (2 Vmod) (3 Verbos_CI) ;
REMOVE Li IF (0 Lu) ;

SELECT Li IF (0 Bi) (1 Lu + Pron) ;	# bi = septentrionale
SELECT Li IF (0 Bi) (1 Nche) (2 Lu + Pron) ;	# bi = septentrionale
SELECT Li IF (0 Si) (1 Ddu + Pron) ;	# li/si = guess ; si = meridionale
SELECT Li IF (0 Si) (1 Nche) (2 Ddu + Pron) ;	# li/si = guess ; si = meridionale
REMOVE Li IF (0 Bi) ;	# septentrionale
REMOVE Li IF (0 Si) ;	# meridionale

# Coordinació
SELECT M IF (-1C Ant + M) (0 ("Andrea")) ;	# Filippo Andrea
SELECT M IF (1C Ant + M) (0 ("Andrea")) ;
SELECT F IF (-1C Ant + F) (0 ("Andrea")) ;
SELECT F IF (1C Ant + F) (0 ("Andrea")) ;

SELECT M IF (0 Noun_NP + M) (0 Noun_NP + F) (-1/* Det + M) ;	# un pirata, i calci, un file, sul fronte jugoslavo, il Lazio / la Lazio
SELECT F IF (0 Noun_NP + M) (0 Noun_NP + F) (-1/* Det + F) ;
SELECT M IF (0 A + M) (0 A + F) (-1/* Det + M) ;
SELECT F IF (0 A + M) (0 A + F) (-1/* Det + F) ;
REMOVE M IF (0 Noun_NP + MF) (0 Noun_NP + F) (-1/* Det + F) ;
REMOVE M IF (0 A + MF) (0 A + F) (-1/* Det + F) ;
REMOVE F IF (0 Noun_NP + MF) (0 Noun_NP + F) (-1/* Det + M) ;
REMOVE F IF (0 A + MF) (0 A + F) (-1/* Det + M) ;

SELECT M IF (0 Noun_NP + M) (0 Noun_NP + F) (-1 A + M) (NOT -1 A + F) ;		# un suo moto
SELECT F IF (0 Noun_NP + M) (0 Noun_NP + F) (-1 A + F) (NOT -1 A + M) ;
SELECT M IF (0 Noun_NP + M) (0 Noun_NP + F) (1 A + M) (NOT 1 A + F) ;		# consigliere diplomatico
SELECT F IF (0 Noun_NP + M) (0 Noun_NP + F) (1 A + F) (NOT 1 A + M) ;
SELECT M IF (0 Noun_NP + M) (0 Noun_NP + F) (1 Adv) (2 A + M) (NOT 2 A + F) ;	# consigliere molto diplomatico
SELECT F IF (0 Noun_NP + M) (0 Noun_NP + F) (1 Adv) (2 A + F) (NOT 2 A + M) ;

SELECT Top IF (-1 ("regione") OR ("provincia") OR ("distretto")) (0 MajIni) ;		# nella Regione Veneto
SELECT Top IF (-2 ("regione") OR ("provincia") OR ("distretto")) (-1 A) (0 MajIni) ;
SELECT Top IF (0 MajIni) (1 CM) (2 Num) (3 Mese) ;	# Milano, 6 luglio

SELECT A IF (-5 Det + &&ConcordaGN) (-4 N + Masc + &&ConcordaGN) (-3 CnjCoo) (-2 Det) (-1 N + Fem) (0 ("lìberu") + Masc) ;	# sos òmines e sas fèminas liberos
SELECT A IF (-4 Det + &&ConcordaGN) (-3 N + Masc + &&ConcordaGN) (-2 CnjCoo) (-1 N + Fem) (0 ("lìberu") + Masc) ;	# sos òmines e fèminas liberos
SELECT N IF (-4 Det + &&ConcordaGN) (-3 N + Masc + &&ConcordaGN) (-2 CnjCoo) (-1 Det) (0 N + Fem) (1 ("lìberu") + Masc) ;	# sos òmines e sas fèminas liberos
SELECT N IF (-3 Det + &&ConcordaGN) (-2 N + Masc + &&ConcordaGN) (-1 CnjCoo) (0 N + Fem) (1 ("lìberu") + Masc) ;	# sos òmines e fèminas liberos

REMOVE A + F IF (-1C Noun_NP + M) (0 A + F) (NOT -1 (META:/[-"«»]/r));
REMOVE A + M IF (-1C Noun_NP + F) (0 A + M) (NOT -1 (META:/[-"«»]/r)) ;
REMOVE A + Pl IF (-1C Noun_NP + Sg) (0 A + Pl) (NOT -1 (META:/[-"«»]/r)) ;
REMOVE A + Sg IF (-1C Noun_NP + Pl) (0 A + Sg) (NOT -1 (META:/[-"«»]/r)) ;
REMOVE N + F IF (0 N + F) (1C A + M) (NOT 0 (META:/[-"«»]/r)) ;			# consigliere comunale
REMOVE N + M IF (0 N + M) (1C A + F) (NOT 0 (META:/[-"«»]/r)) ;
REMOVE N + Pl IF (0 N + Pl) (1C A + Sg) (NOT 0 (META:/[-"«»]/r)) ;		# consigliere comunale
REMOVE N + Sg IF (0 N + Sg) (1C A + Pl) (NOT 0 (META:/[-"«»]/r)) ;

SELECT Sg IF (0 Noun_NP + Sg) (0 Noun_NP + Pl) (-1/* Det + Sg) ;
SELECT Pl IF (0 Noun_NP + Sg) (0 Noun_NP + Pl) (-1/* Det + Pl) ;
SELECT Sg IF (0 A + Sg) (0 A + Pl) (-1/* Det + Sg) ;
SELECT Pl IF (0 A + Sg) (0 A + Pl) (-1/* Det + Pl) ;
REMOVE Sg IF (0 Noun_NP + SP) (0 Noun_NP + Pl) (-1/* Det + Pl) ;
REMOVE Sg IF (0 A + SP) (0 A + Pl) (-1/* Det + Pl) ;
REMOVE Pl IF (0 Noun_NP + SP) (0 Noun_NP + Pl) (-1/* Det + Sg) ;
REMOVE Pl IF (0 A + SP) (0 A + Pl) (-1/* Det + Sg) ;

# NP

REMOVE Cog IF (0 ("<Costa>")) (1 A + FemSg) ;
SELECT N IF (-1/* Det + Def + MascSg) (0 ("<Cantone>")) ;	# il Cantone

SELECT NP IF (0 NP) (NOT -1 BOS) (NOT 0 ("Conte") OR ("Duca") OR ("Sarbadore") OR ("Mannu") OR ("Monte") OR ("Romanos")) ;
	# Llevem paraules que, per una raó o altra, es posen en majúscules sense ser NP
SELECT NP IF (0 ("Val")) (0 (META:/-/r)) ;
SELECT NP IF (0 ("Val")) (1 ("-")) ;
SELECT NP IF (0 ("Puddu")) ;
SELECT A IF (-1 Top + MascSg) (0 ("Mannu")) ;
SELECT A IF (-1 Top + &&ConcordaGV) (0 MajIni + &&ConcordaGV) ;
SELECT A IF (-1 Ant + MascSg) (0 ("Mannu")) ;
SELECT A IF (-2 Ant + MascSg) (-1 Det + Def + MascSg) (0 ("Mannu")) ;

REMOVE NP IF (0 ("Duca") OR ("Conte") OR ("Contessa") OR ("Barone")) (1 Ant OR unknownMajIni) ;
SELECT Ant IF (-1 ("paba") OR ("imperadore") OR ("tzar") OR ("re") OR ("giuighe") OR ("giuighissa") OR ("prìntzipe") OR ("duca") OR ("archiduca") OR ("marchesu") OR ("conte") OR ("visconte") OR ("barone")) (0 Ant) ;
SELECT Ant OR Cog IF (-1 ("presidente") OR ("guvernaore") OR ("piscamu") OR ("archipiscamu")) (0 Ant) ;

REMOVE NP IF (-1/* Det + MascSg) (0 Noun_Adj + MascSg) ;
REMOVE NP IF (-1/* Det + FemSg) (0 Noun_Adj + FemSg) ;
REMOVE NP IF (-1/* Det + FemPl) (0 Noun_Adj + FemPl) ;

REMOVE NP IF (0 ("Duca") OR ("Conte") OR ("Barone")) (1 De) (2 Top OR ("San") OR unknownMajIni) ;
SELECT Sg IF (0 ("sennore") OR ("marchesu")) (1 Ant OR unknownMajIni) (NOT 1 Ant + Fem) (NOT -1/* Pl) ;
SELECT Sg IF (0 ("sennore") OR ("marchesu")) (1 De) (2 Top OR ("San") OR unknownMajIni) (NOT -1/* Pl) ;

REMOVE NP IF (-2 Ant) (-1 Det + Def + MascSg) (0 NP) (0 Noun_Adj + MascSg) ;	# Plinio il Vecchio
REMOVE NP IF (-2 Ant) (-1 Det + Def + FemSg) (0 NP) (0 Noun_Adj + FemSg) ;	# Caterina la Grande

SELECT A IF (-1 Noun_Adj) (0 ("<USA>")) ;			# le sale cinematografiche USA
SELECT N IF (-1/* Det + &&ConcordaGN) (0 NP) (0 N + &&ConcordaGN) (1C A + &&ConcordaGN) ;
SELECT N IF (-1/* Det + MascPl) (0 NP) (0 N + MascPl) (1C A + MascPl) ;
SELECT N IF (-1/* Det + FemSg) (0 NP) (0 N + FemSg) (1C A + FemSg) ;
SELECT N IF (-1/* Det + FemPl) (0 NP) (0 N + FemPl) (1C A + FemPl) ;

SELECT NP IF (0 Top) (0 N OR A) (NOT -1C (ant) OR (cog));


REMOVE Cog IF (-1 In OR Dae) (0 Top) (0 Cog) ;
REMOVE Ant IF (-1 In OR Dae) (0 Top) (0 Ant) ;

SELECT NP IF (0 N OR NP) (1 NP) (0 (META:/-/r)) ;	# Pierre-Michel
SELECT Ant IF (0 Ant) (1C Ant) (0 (META:/-/r)) ;	# Jean-René
SELECT Ant IF (-1C Ant) (0 Ant) (-1 (META:/-/r)) ;
SELECT Cog IF (0 Cog) (1C Cog) (0 (META:/-/r)) ;
SELECT Cog IF (-1C Cog) (0 Cog) (-1 (META:/-/r)) ;
SELECT Top IF (0 Top) (1C Top) (0 (META:/-/r)) ;
SELECT Top IF (-1C Top) (0 Top) (-1 (META:/-/r)) ;
SELECT Top IF (NOT -2 Ant OR Cog) (-1 Top) (0 Top) (-1 (META:/-/r)) ;	# Milano-Napoli
SELECT Top IF (NOT -1 BOS) (0 NP) (1 CM) (2 Anno) ; # – Burgos, 1259

SELECT Ant IF (0 ("Tzèsare")) (-1 BOS) ;

REMOVE Ant_Cog IF (-1 Det + Def) (0 Top) ;
SELECT Ant IF (0 Ant) (1C Ant) ;	# Rosa Maria
SELECT Ant IF (0 Ant) (1 Cog) (NOT -1 Ant) ;	# Giordano Bruno
SELECT Ant IF (-1 ("santo")) (0 Ant) (0 Cog OR Top) ;	# sant Benet
SELECT Cog IF (0 Cog OR Ant) (-1C Ant) (NOT 1 segles_num_romans) (NOT 1 Cog) (NOT 1 De);
SELECT Ant_Cog IF (0 Cog OR Ant) (1C Cog) (NOT 0 ("Lago") OR ("Val") OR ("Monte") OR ("Monti") OR ("Torre")) ;
SELECT Ant_Cog IF (0 Cog OR Ant) (1 unknownMajIni) (NOT 0 ("Lago") OR ("Val") OR ("Monte") OR ("Monti") OR ("Torre")) ;
SELECT Ant_Cog IF (-1 unknownMajIni) (0 Cog OR Ant) ;
SELECT Ant IF (0 Ant) (0 Cog) (1 De) (2 Top OR unknownMajIni) ;	# Alfonso di Portogallo
SELECT Top IF (-2 Ant) (-1 De) (0 Ant OR Cog OR Top) ;	# Ipparco di Nicea
SELECT Top IF (-3 Ant) (-2 segles_num_romans) (-1 De) (0 Ant OR Cog OR Top) ;	# Joan II di Castiglia
SELECT Top IF (-2 ("cuntzìliu") OR ("parlamentu") OR ("istatutu") OR ("costitutzione") OR ("tratadu") OR ("declaratzione"))
	(-1 De) (0 Ant OR Cog OR Top) ;
	# contzìliu di Nicea
SELECT Top IF (-2 ("corona") OR ("regnu") OR ("contea") OR ("ducadu")) (-1 De) (0 Cog OR Top) ;	# regno di Castiglia
SELECT Top IF (-2 Dignidade) (-1 De) (0 Ant OR Cog ) ;	# principe di Viana
SELECT Top IF (-2 ("tzitade") OR ("aeroportu") OR ("portu") OR ("rennu") OR ("giuigadu") OR ("printzipadu") OR ("granducadu") OR ("ducadu") OR ("marchesadu") OR ("contea") OR ("baronia") OR ("segnoria") OR ("califadu") OR ("emiradu") OR ("sultanadu") OR ("khanadu") OR ("satrapia") OR ("despotadu") OR ("repùblica") OR ("artzpiscamadu") OR ("piscamadu")) (-1 De) (0 Cog OR Ant) ;
REMOVE Ant_Cog IF (-1 Det + Def) (0 NP) ;	# la Roma, il Veneto
REMOVE Ant_Cog IF (-1 ("in")) (0 NP) ;		# in Napoli
SELECT Top IF (-2 ("andare") OR ("biagiare")) (-1 ("a")) (0 Ant OR Cog OR Top) ;
#REMOVE Top IF (NOT -2 ("andare") OR ("biagiare")) (-1 ("a")) (0C Ant OR Cog OR Top) ;
SELECT Top IF (-3 ("andare") OR ("biagiare")) (-2 ("a")) (-1 Det + Def) (0 Ant OR Cog OR Top) ;
#REMOVE Top IF (NOT -3 ("andare") OR ("biagiare")) (-2 ("a")) (-1 Det + Def) (0C Ant OR Cog OR Top) ;
SELECT Top IF (-2 NP) (-1 De) (0 Ant OR Cog OR Top) ;

SELECT Top IF (-2C Top) (-1 CM OR CnjCoo) (0 Top) (0 Cog OR Ant);
SELECT Top IF (2 Top) (1 CM OR CnjCoo) (0 Top) (0 Cog OR Ant);
SELECT Top IF (-2C Top) (-1 CM OR CnjCoo) (0 Top) (0 Cog OR Ant);
SELECT Ant OR Cog IF (-2C Ant OR Cog) (-1 CM OR CnjCoo) (0 Top) (0 Cog OR Ant);
SELECT Ant OR Cog IF (2 Ant OR Cog) (1 CM OR CnjCoo) (0 Top) (0 Cog OR Ant);
SELECT Ant IF (-2C Ant) (-1 CM OR CnjCoo) (0 Cog) (0 Ant);
SELECT Ant IF (2C Ant) (1 CM OR CnjCoo) (0 Cog) (0 Ant);
SELECT Cog IF (-2C Cog) (-1 CM OR CnjCoo) (0 Cog) (0 Ant);
SELECT Cog IF (2C Cog) (1 CM OR CnjCoo) (0 Cog) (0 Ant);

# A O ADV

SELECT Adv IF (-1 N + &&ConcordaGN) (0 ("<pius>")) (1 A + &&ConcordaGN) ; # frutos pius bonos
SELECT Adv IF (-1 Det + Def + &&ConcordaGN) (0 Prus) (1 A + &&ConcordaGN) ; # su prus fidele possìbile
SELECT Adv IF (-1 Det + Def) (0 Prus) (1 Adv) ; # su prus fidelemente possìbile
SELECT Adv IF (0 Prus) (1 unknown) ;
SELECT A OR Adv IF (-2 Det + Def + &&ConcordaGN) (-1 Prus) (0 A + &&ConcordaGN) ; # su prus fidele possìbile

SELECT Adv IF (-1 N + &&ConcordaGN) (0 Mesu) (1 Adj_PP + &&ConcordaGN) ;  
SELECT Adv IF (0 Mesu) (1C PP) ;

SELECT A IF (-2/* Det + Def + &&ConcordaGN) (-1 N + &&ConcordaGN) (0 ("matessi")) ;	# sos omines matessi produint
SELECT Adv_PreAdv IF (-1 N + &&ConcordaGN) (0 ("bastante")) (1 A + &&ConcordaGN) ;	# una connoschentzia bastante bona

# A O NUM

REMOVE Num IF (0 Aord + &&ConcordaGN) (1 N + &&ConcordaGN) ;    # 2º posto

# A O PRN

REMOVE Pron IF (0 ("cale si siat")) (1C N) ;    # cale si siat ala

# A O PREP

REMOVE A IF (NOT -1C Prep) (0 Prep) (NOT 0 Aord) (1C N_A) ;	# finas gente comuna
REMOVE A IF (NOT -1C Prep) (0 Prep) (NOT 0 Aord) (1 N) (0 A + &&ConcordaGN) (NOT 1 N + &&ConcordaGN) ;	# finas gente comuna

# N OR V

REMOVE Inf OR V + Pers IF (-1C/* Det + &&ConcordaGN) (0 N + &&ConcordaGN) ;	# su pòtere
REMOVE Inf OR V + Pers IF (-1/* Det + Def + &&ConcordaGN) (0 N + &&ConcordaGN) ;	# su pòtere
SELECT Inf IF (-2/* Det + Def + MascSg) (-1 De) (0 Inf) ;	# su de pòtere

REMOVE PP IF (-1C/* Det + &&ConcordaGN) (0 N + &&ConcordaGN) ;	# de s'istadu

# N OR PRON

SELECT Pron IF (-1C Barrera_Inicial_Estrict_SN) (0 ("medas")) (1C V + P3 + Pl) ;	# medas cantant
SELECT Pron IF (-1C Barrera_Inicial_Estrict_SN) (0 ("medas")) (1 Pro) (2C V + P3 + Pl) ;
SELECT Pron IF (-1C Barrera_Inicial_Estrict_SN) (0 ("medas")) (1 Pro) (2 Pro) (3C V + P3 + Pl) ;

# N O A

SELECT N IF (-1 Tra) (1 ("e")) (2C N) ;
SELECT N IF (-3 Tra) (-2C N) (-1 ("e")) ;

SELECT A IF (-2 Num) (-1 ("e")) (0 ("mesu")) ;	# sas chimbe e mesa de merie

SELECT A IF (-4 N + &&ConcordaGN) (-3 A + &&ConcordaGN) (-2 CnjCoo) (-1 A + &&ConcordaGN) (0 ("mannu")) ;	# un'isvilupu industriale e turìsticu mannos/mannu

SELECT A IF (-2 Ant) (-1 Det + Def + MascSg) (0 A + MascSg + MajIni) ;  # Ludovicu su Piu
SELECT A IF (-2 Ant) (-1 Det + Def + FemSg) (0 A + FemSg + MajIni) ;

REMOVE A IF (0 A) (0 N) (-1/* Det) (1C Barrera_Final_SN) ; 	# una forza valida nel mondo. WARNING: No funciona per a "nel", però sí per a "el" (problema CG)
REMOVE A IF (0 A) (0 N) (-2/* Det) (-1C A) (1C Barrera_Final_SN) ; 		# nei seguenti comuni
REMOVE A IF (0 A) (0 N) (-2/* Det) (-1C A) (1 CnjCoo) (NOT 2 A) ;
REMOVE A IF (0 A) (0 N) (-2 CnjCoo) (-1C A) (1C Barrera_Final_SN) ;		# e grande amico
REMOVE A IF (0 A) (0 N) (-1/* Prep) (1C Barrera_Final_SN) ; 			# solució problema "nel" (a més tracta el cas: Prep N/A.
REMOVE A IF (0 A) (0 N) (-1/* Prep) (1 CnjCoo) (2C Prep) ; 			# tempo di record e nel giro
REMOVE PP IF (0 PP) (0 N) (NOT -1 ("molto")) (-1/* Det) (1C Barrera_Final_SN) ;
REMOVE PP IF (0 PP) (0 N) (-1/* Prep) (1C Barrera_Final_SN) ;

REMOVE A IF (0 A) (0 N) (NOT 0 Aord OR AdjN) (-1/* Det OR Num) (1 A) (1 N) (2 Barrera_Final_Estrict_SN) ; 	# dello Stato spagnolo. / il primo amico
REMOVE A IF (0 A) (0 N) (NOT 0 Aord OR AdjN) (-1/* Prep) (1 A) (1 N) (2 Barrera_Final_Estrict_SN) ; 	# dello Stato spagnolo. / il primo amico
REMOVE N IF (0 A) (0 N) (NOT -1 Aord OR AdjN) (-2/* Det) (-1 N) (1 Barrera_Final_Estrict_SN) ; 				# dello Stato spagnolo. / il primo amico
REMOVE N IF (0 A) (0 N) (NOT -1 Aord OR AdjN) (-2/* Prep) (-1 N) (1 Barrera_Final_Estrict_SN) ; 			# dello Stato spagnolo. / il primo amico

SELECT N IF (0 A) (0 N + &&ConcordaGN) (NOT 0 Aord OR AdjN) (-1 Barrera_Inicial_Estrict_SN) (1 A + &&ConcordaGN) (1 N) (2 Barrera_Final_Estrict_SN) ;
SELECT Aord IF (0 N) (0 Aord + &&ConcordaGN) (-1 Barrera_Inicial_Estrict_SN) (1 N + &&ConcordaGN) (1 A) (2 Barrera_Final_Estrict_SN) ;
SELECT Aord IF (0 N) (0 Aord + &&ConcordaGN) (-1 Barrera_Inicial_Estrict_SN) (1 N + &&ConcordaGN) (1 PP) (2 Barrera_Final_Estrict_SN) (NOT 2 Dae) ;

SELECT Aord IF (-1 Det + &&ConcordaGN) (0 Aord + &&ConcordaGN) (1C N + &&ConcordaGN) ;

REMOVE A IF (0 A) (0 N) (NOT 0 Aord OR AdjN) (-1/* Det) (1 A) (1 N) (2 CM OR Ant OR Cog) ; 	# allo stato fossile,
REMOVE A IF (0 A) (0 N) (NOT 0 Aord OR AdjN) (-1/* Prep) (1 A) (1 N) (2 CM OR Ant OR Cog) ; 	# allo stato fossile,
REMOVE N IF (0 A) (0 N) (-2/* Det) (-1 N) (NOT -1 Aord OR AdjN) (1 CM) ; 			# allo stato fossile,
REMOVE N IF (0 A) (0 N) (-2/* Prep) (-1 N) (NOT -1 Aord OR AdjN) (1 CM) ; 			# allo stato fossile,

REMOVE Adj_PP IF (0 Adj_PP) (0 N) (-1/* Det) (1C Vall) ; 				# il comune nacque
REMOVE Adj_PP IF (0 Adj_PP) (0 N) (-1/* Prep) (1C Vall) ; 				# il comune nacque / di media durata
REMOVE Adj_PP IF (0 Adj_PP) (0 N) (-1/* Det) (1 Adv) (2 Vall) ; 			# il comune non nacque
REMOVE Adj_PP IF (0 Adj_PP) (0 N) (-1/* Prep) (1 Adv) (2 Vall) ; 			# il comune non nacque

REMOVE Adj_PP IF (0 Adj_PP) (0 N + Pl) (-1 Num) (1C Barrera_Final_Estrict_SN OR Vall) ; 	# I 5 comuni facenti parte
REMOVE Adj_PP IF (0 Adj_PP) (0 N + Pl) (-1 Num) (NOT 0 Aord OR AdjN) ;

REMOVE A IF (-1/* Det + &&ConcordaGN) (0 A + &&ConcordaGN) (0 N + &&ConcordaGN) (NOT 1 N + &&ConcordaGN OR CnjCoo) ;	# distinguere gli ambiti e usi
SELECT A IF (-1/* Det + Def + &&ConcordaGN) (0 Aord + &&ConcordaGN) ;	# il settimo e ultimo re

SELECT A IF (-1/* Det + &&ConcordaGN) (0 AdjN + &&ConcordaGN) (1 N + &&ConcordaGN) ;	# un'àtera informatzione

REMOVE Adj_PP IF (-1/* Det + &&ConcordaGN) (0 N + &&ConcordaGN) (1 A + Pos + &&ConcordaGN) ;	# s'istadu suo

REMOVE A IF (0 A) (0 N) (-1/* Det) (1 A) (1 N) (2 Lpar) ; 	#
REMOVE A IF (0 A) (0 N) (-1/* Prep) (1 A) (1 N) (2 Lpar) ; 	#
REMOVE N IF (0 A) (0 N) (-2/* Det) (-1 N) (1 Lpar) ; 		#
REMOVE N IF (0 A) (0 N) (-2/* Prep) (-1 N) (1 Lpar) ; 		# per miglio quadrato (228,5/km²)

REMOVE A IF (0 A) (0 N) (-1/* Det) (1C A) (NOT 0 AdjN) ; 	# dello Stato Pontificio
REMOVE A IF (0 A) (0 N) (-1/* Prep) (1C A) (NOT 0 AdjN) ; 	# dello Stato Pontificio

REMOVE A IF (0 A) (NOT 0 Aord OR AdjN) (0 N) (-1/* Det) (1 A) (1 N) (2C Prep) ; 	# dello stato messicano di Coahuila / nei primi decenni
REMOVE A IF (0 A) (NOT 0 Aord OR AdjN) (0 N) (-1/* Prep) (1 A) (1 N) (2C Prep) ;# dello stato messicano di Coahuila / nei primi decenni
REMOVE N IF (0 Aord OR AdjN) (0 N) (-1/* Det) (1 A) (1 N) (2C Prep) ; 		# dello stato messicano di Coahuila / nei primi decenni
REMOVE N IF (0 Aord OR AdjN) (0 N) (-1/* Prep) (1 A) (1 N) (2C Prep) ; 		# dello stato messicano di Coahuila / nei primi decenni
REMOVE N IF (0 A) (0 N) (-2/* Det) (-1 N) (1 Prep) (NOT 1 Dae) ;	# dello stato messicano di Coahuila
REMOVE N IF (0 A) (0 N) (-2/* Prep) (-1 N) (1 Prep) (NOT 1 Dae) ;	# dello stato messicano di Coahuila
REMOVE N IF (0 A) (0 N) (-2/* Det) (-1 N) (1 Dae) ; 		# il titolo assunto da Filippo
REMOVE A IF (0 A) (0 PP) (-2/* Det) (-1 N) (1 Dae) ;		# il titolo assunto da Filippo
REMOVE A IF (0 A) (0 PP) (-3/* Det) (-2 N) (-1 A) (1 Dae) ;

REMOVE A IF (0 A) (0 N) (-1C Aord OR AdjN OR Det) (1 EOS OR CM OR Prep OR CnjSub OR CnjAdv) ;		# nei primi decenni del Novecento
REMOVE Adv IF (0 Adv) (0 N) (-1C Aord OR AdjN OR Det) (1 EOS OR CM OR Prep OR CnjSub OR CnjAdv) ;	# il quinto piano

REMOVE A IF (0 A) (0 N) (-1/* Det) (1 A) (1 N) (2/* Prep) ; 		# dello stato tedesco del Baden-Württemberg
REMOVE A IF (0 A) (0 N) (-1/* Prep) (1 A) (1 N) (2/* Prep) ; 		# dello stato tedesco del Baden-Württemberg
REMOVE N IF (0 A) (0 N) (-2/* Det) (-1 N) (1/* Prep) ; 	# dello stato tedesco del Baden-Württemberg
REMOVE N IF (0 A) (0 N) (-2/* Prep) (-1 N) (1/* Prep) ; 	# dello stato tedesco del Baden-Württemberg
REMOVE A IF (0 A) (0 N) (-1/* Det) (1 A) (1 N) (2/* Det) ; 		# dello stato tedesco del Baden-Württemberg
REMOVE A IF (0 A) (0 N) (-1/* Prep) (1 A) (1 N) (2/* Det) ; 		# dello stato tedesco del Baden-Württemberg
REMOVE N IF (0 A) (0 N) (-2/* Det) (-1 N) (1/* Det) ; 	# dello stato tedesco del Baden-Württemberg
REMOVE N IF (0 A) (0 N) (-2/* Prep) (-1 N) (1/* Det) ; 	# dello stato tedesco del Baden-Württemberg

REMOVE A IF (0 A) (0 N) (-1/* Det) (1/* Prep) ; 		# sullo stato dei mari
REMOVE A IF (0 A) (0 N) (-1/* Prep) (1/* Prep) ; 		# sullo stato dei mari
REMOVE A IF (0 A) (0 N) (-1/* Det) (1/* Det) ; 			# sullo stato dei mari
REMOVE A IF (0 A) (0 N) (-1/* Prep) (1/* Det) ; 		# sullo stato dei mari

REMOVE A IF (0 A) (0 N) (-1/* Det) (1 A) (NOT 0 Aord OR AdjN) ; # lo stato italiano
REMOVE Adv IF (0 Adv) (0 N) (-1/* Det) (1 A) ; 			# un'altra specie chimica

REMOVE N IF (0 AdjN) (1 unknown) ;

REMOVE N IF (-1 Top + &&ConcordaGN) (0 A + &&ConcordaGN + MajIni) ;
REMOVE N IF (-2 Det + Def + &&ConcordaGN) (-1 Top + &&ConcordaGN) (0 A + &&ConcordaGN) ;

REMOVE A IF (0 N + Sg + &&ConcordaGN) (1 A + Pos + &&ConcordaGN) (-1 Ser + Sg OR Cunsiderare + Sg OR Giudicare + Sg OR Sentire + Sg OR Resurtare + Sg OR Mantenere + Sg + Abarrare + Sg OR Parrere + Sg) ;	# est amigu meu
REMOVE A IF (0 N + Pl + &&ConcordaGN) (1 A + Pos + &&ConcordaGN) (-1 Ser + Pl OR Cunsiderare + Pl OR Giudicare + Pl OR Sentire + Pl OR Resurtare + Pl OR Mantenere + Pl + Abarrare + Pl OR Parrere + Pl) ;	# sunt amigos meos
REMOVE A IF (0 N + Sg + &&ConcordaGN) (1 A + Pos + &&ConcordaGN) (-2 Ser + Sg OR Cunsiderare + Sg OR Giudicare + Sg OR Sentire + Sg OR Resurtare + Sg OR Mantenere + Sg + Abarrare + Sg OR Parrere + Sg) (-1 Adv) ;	# est semper amigu meu
REMOVE A IF (0 N + Pl + &&ConcordaGN) (1 A + Pos + &&ConcordaGN) (-2 Ser + Pl OR Cunsiderare + Pl OR Giudicare + Pl OR Sentire + Pl OR Resurtare + Pl OR Mantenere + Pl + Abarrare + Pl OR Parrere + Pl) (-1 Adv) ;	# sunt semper amigos meos
REMOVE N IF (0 N + Sg) (0 A + Sg) (-1 Ser + Sg OR Cunsiderare + Sg OR Giudicare + Sg OR Sentire + Sg OR Resurtare + Sg OR Mantenere + Sg + Abarrare + Sg OR Parrere + Sg) ;	# è verde
REMOVE N IF (0 N + Pl) (0 A + Pl) (-1 Ser + Pl OR Cunsiderare + Sg OR Giudicare + Sg OR Sentire + Sg OR Resurtare + Sg OR Mantenere + Sg + Abarrare + Sg OR Parrere + Pl) ;
REMOVE N IF (0 N + Sg) (0 A + Sg) (-2 Ser + Sg OR Cunsiderare + Sg OR Giudicare + Sg OR Sentire + Sg OR Resurtare + Sg OR Mantenere + Sg + Abarrare + Sg OR Parrere + Sg) (-1 Adv_PreAdv OR CnjAdv) ;	# è molto verde
REMOVE N IF (0 N + Pl) (0 A + Pl) (-2 Ser + Pl OR Cunsiderare + Sg OR Giudicare + Sg OR Sentire + Sg OR Resurtare + Sg OR Mantenere + Sg + Abarrare + Sg OR Parrere + Pl) (-1 Adv_PreAdv OR CnjAdv) ;	# sono però piuttosto piatti
REMOVE N IF (0 N + Sg) (0 A + Sg) (-3 Ser + Sg OR Cunsiderare + Sg OR Giudicare + Sg OR Sentire + Sg OR Resurtare + Sg OR Mantenere + Sg + Abarrare + Sg OR Parrere + Sg) (-2 Adv_PreAdv OR CnjAdv) (-1 Adv_PreAdv) ;
REMOVE N IF (0 N + Pl) (0 A + Pl) (-3 Ser + Pl OR Cunsiderare + Sg OR Giudicare + Sg OR Sentire + Sg OR Resurtare + Sg OR Mantenere + Sg + Abarrare + Sg OR Parrere + Pl) (-2 Adv_PreAdv OR CnjAdv) (-1 Adv_PreAdv) ;

SELECT N IF (-2 Barrera_Inicial_Estrict_SN) (-1 A + &&ConcordaGN) (NOT -1 N + &&ConcordaGN) (0 N + &&ConcordaGN) (1 Barrera_Final_Estrict_SN OR A) ;
SELECT N IF (-2/* Det + &&ConcordaGN) (-1 A + &&ConcordaGN) (NOT -1 N + &&ConcordaGN) (0 N + &&ConcordaGN) (1 Barrera_Final_Estrict_SN OR A) ;
SELECT N IF (-2 Barrera_Inicial_Estrict_SN) (-1 A + &&ConcordaGN) (NOT -1 N + &&ConcordaGN) (NOT 0 Aord OR AdjN) (0 N + &&ConcordaGN) (1 A + &&ConcordaGN) ;
SELECT N IF (-2/* Det + &&ConcordaGN) (-1 A + &&ConcordaGN) (NOT -1 N + &&ConcordaGN) (0 N + &&ConcordaGN) (NOT 0 Aord OR AdjN) (1 A + &&ConcordaGN) ;

SELECT N IF (-2/* Det + &&ConcordaGN) (-1 A + &&ConcordaGN) (NOT -1 Adv OR PreAdv) (0 N + &&ConcordaGN) (NOT 0 Aord OR AdjN) (1 CM) ;   # lontano dalla sua patria,
SELECT N IF (-2/* Det + &&ConcordaGN) (-1 A + &&ConcordaGN) (0 N + &&ConcordaGN) (NOT 0 Aord OR AdjN) (1 CM) (NOT 2 A + &&ConcordaGN) ; # lontano dalla sua patria,

REMOVE N IF (-1C N + &&ConcordaGN) (0 A + &&ConcordaGN) ;
REMOVE N IF (-2C N + &&ConcordaGN) (-1 A + &&ConcordaGN) (0 A + &&ConcordaGN) ;
REMOVE N IF (-3C N + &&ConcordaGN) (-2 A + &&ConcordaGN) (-1 A + &&ConcordaGN) (0 A + &&ConcordaGN) ;

REMOVE A IF (0 A) (0 N) (-1C V) (1/* Det) ; 			# porre fine a

REMOVE A IF (0 A) (0 N) (-1 ("àere")) (NOT 1 A) ;
REMOVE A IF (0 A) (0 N) (-2 ("àere")) (-1 Adv) (NOT 1 A) ;	# non ebbe più dubbi

REMOVE Adv IF (0 Adv) (0 N + &&ConcordaGN) (-1/* Det + &&ConcordaGN) (NOT 1 A + &&ConcordaGN) (NOT -1 PreAdv) ; 	# ogni insieme / molto male

SELECT N IF (0 N) (0 PP) (-1C Det) (1 unknown) ;
SELECT N IF (0 N) (0 A) (-1C Det) (1 unknown) ;			# Secondo la rivista Motorrader
SELECT Npl IF (0 Npl) (0 PP) (-1C Num) (1 unknown) ;
SELECT Npl IF (0 Npl) (0 A) (-1C Num) (1 unknown) ;

#SELECT A IF (0 A) (0 N) (1 A) (-1C N) ; 			# Regno Lombardo-Veneto (no funciona: il presidente Franco Marini)

REMOVE A IF (0 A) (0 N) (-1 Pos) (1 EOS) ;		# loro sorte.
REMOVE PP IF (0 PP) (0 N) (-1 Pos) (1 EOS) ;		# loro sorte.
REMOVE Vconj IF (0 Vconj) (0 N) (-1C A) (1 EOS) ;	# le più grandi collezioni.
REMOVE Vconj IF (0 Vconj) (0 N) (-1C A) (1 Lpar) ;	# due nuove canzonii (di cui una

REMOVE A IF (0 A) (0 N) (-1 Pos) (1 Prep) ; 		# il suo futuro attraverso una programmazione
REMOVE PP IF (0 PP) (0 N) (-1 Pos) (1 Prep) ;
REMOVE A IF (0 A) (0 N) (-1/* Det) (1 Prep) ; 		# il futuro della Ue.
REMOVE PP IF (0 PP) (0 N) (-1/* Det) (1 Prep) ;

REMOVE PP IF (0 PP) (0 N) (-1C Prep) ;		# in corso "Pitretu è Bicchisgià"
REMOVE PP IF (0 PP) (0 A) (-1C Prep) ;
REMOVE PP IF (0 PP) (0 A) (-1C N) (NOT 1 Dae OR Comente) ;	# nucleo abitato
REMOVE PP IF (0 PP) (0 A) (-1C V) ;		# si rivelò corretta

REMOVE PP IF (0 PP + &&ConcordaGN) (0 A + &&ConcordaGN) (-2 N + &&ConcordaGN) (-1 A + &&ConcordaGN) (NOT 1/* Dae OR Comente) ;	# geometria algebrica astratta

REMOVE A IF (0 PP + &&ConcordaGN) (0 A + &&ConcordaGN) (-2 N + &&ConcordaGN) (-1 A + &&ConcordaGN) (1/* Dae OR Comente) ;	# informazioni autentiche scritte dai cronisti

SELECT PP IF (-1C N + &&ConcordaGN) (0C V) (0 PP + &&ConcordaGN) ;	# delle vesti schiacciate sul corpo

REMOVE Adv IF (-1 Barrera_Inicial_Estrict_SN) (0 A + &&ConcordaGN) (0 Adv) (1C N + &&ConcordaGN) ;	# Primo mandato

REMOVE V IF (0 V) (0 A) (-1C Det) ;		# i celebri laghi
REMOVE V IF (0 V) (0 N) (-1C Det) ;

REMOVE V IF (NOT -2 N) (-1C A + &&ConcordaGN) (0 N + &&ConcordaGN) (0 V) ;	# primo violino

REMOVE Ifi IF (-2 N) (-1 ("più") OR ("meno")) (0 Ifi OR A OR PP) ;		# lingue più diffuse
REMOVE Ifi IF (-3 N) (-2 A) (-1 ("più") OR ("meno")) (0 Ifi OR A OR PP) ;	# lingue internazionali più diffuse
SELECT A IF (-2 N) (-1 ("più") OR ("meno")) (0 A OR PP) ;			# lingue più diffuse
SELECT A IF (-3 N) (-2 A) (-1 ("più") OR ("meno")) (0 A OR PP) ;		# lingue internazionali più diffuse

SELECT A IF (0 Pos) (-1/* Det) (1 N) ;	# il suo governo [regla necesaria porque "il suo" y similares no se cogen como un lema en ita-srd]
					# WARNING: No funciona per a "del suo governo" (igual que a dalt amb "nel mondo")
SELECT A IF (0 Pos) (-1/* Prep) (1 N) ;	# solució problema "del"

REMOVE V IF (0 N) (0 Vconj) (-1 Prep) ;	# la rivolta dei Fasci siciliani.
REMOVE V IF (0 N) (0 Vconj) (-1C Det) ;	# in un dato

REMOVE Vconj IF (0 Vconj) (0 N) (-1 BOS) (1C A) (2 EOS) ;	# Relazioni internazionali

SELECT N IF (0 Noun_Adj_PP LINK NOT 0 NP OR ("ser") OR Prep) (-1C Det) (1 Barrera_Final_Estrict_SN) ;
SELECT N IF (0 Noun_Adj_PP LINK NOT 0 NP OR ("ser") OR Prep) (-1C Det) (1 CM OR CnjCoo) (1 Barrera_Final_Estrict_SN) ;
SELECT N + &&ConcordaGN IF (-1C Barrera_Inicial_Estrict_SN) (0 N + &&ConcordaGN) (1 A + &&ConcordaGN) (2C Barrera_Final_Estrict_SN) ;	# note tecniche

REMOVE Prep IF (1 EOS) ;

# COORDINAZIONE

REMOVE Pl IF (-1C/* Det + Sg) (0 Noun_Adj_PP + Sg) (0 Noun_Adj_PP + Pl) ;	# e la morte.
REMOVE Sg IF (-1C/* Det + Pl) (0 Noun_Adj_PP + Sg) (0 Noun_Adj_PP + Pl) ;
REMOVE M IF (-1C/* Det + Fem) (0 Noun_Adj_PP + Fem) (0 Noun_Adj_PP + Masc) ;
REMOVE F IF (-1C/* Det + Masc) (0 Noun_Adj_PP + Fem) (0 Noun_Adj_PP + Masc) ;

# NOM CONJ NOM

REMOVE A OR PP IF (-1/* Det + &&ConcordaGN) (0 Noun_Adj_PP + &&ConcordaGN LINK 0 N OR NP) (1 CnjCoo) (2C N) ;

# ADJ CONJ ADJ

REMOVE N OR PP IF (-3 N + &&ConcordaGN) (-2C A + &&ConcordaGN) (-1 CnjCoo) (0 Noun_Adj_PP + &&ConcordaGN LINK 0 N OR NP) ;	# genere musicale e teatrale

REMOVE A OR PP IF (-1 CnjCoo) (0 Noun_Adj_PP LINK 0 N OR NP) (1 CnjCoo) (2C N) ;
REMOVE A OR PP IF (-3 CnjCoo) (-2C N) (-1 CnjCoo) (0 Noun_Adj_PP LINK 0 N OR NP) ;	# o prodotto o persona

REMOVE A OR PP IF (-1/* Det + &&ConcordaGN) (0 Noun_Adj_PP + &&ConcordaGN LINK 0 N OR NP) (1 CnjCoo) (2 Noun_Adj_PP LINK 0 N OR NP) (3 Barrera_Final_SN) ;
REMOVE A OR PP IF (-1/* Det + &&ConcordaGN) (0 Noun_Adj_PP + &&ConcordaGN LINK 0 N OR NP) (1 CnjCoo) (2 Det) (3 Noun_Adj_PP LINK 0 N OR NP) (4 Barrera_Final_SN) ;

REMOVE A OR PP IF (-1 Barrera_Inicial_SN LINK NOT 0 N) (NOT -1 Limite_Barrera_Inicial_SN) (0 Noun_Adj_PP LINK 0 N OR NP) (1 CnjCoo) (2 Noun_Adj_PP LINK 0 N OR NP) (3 Barrera_Final_SN) ;
REMOVE A OR PP IF (-1 Barrera_Inicial_SN LINK NOT 0 N) (0 Noun_Adj_PP LINK 0 N OR NP) (1 CnjCoo) (2C N) (3 Barrera_Final_SN) ;
REMOVE A OR PP IF (-3 Barrera_Inicial_SN LINK NOT 0 N) (NOT -3 Limite_Barrera_Inicial_SN) (-2 Noun_Adj_PP LINK 0 N OR NP) (-1 CnjCoo) (0 Noun_Adj_PP LINK 0 N OR NP) (1 Barrera_Final_SN) ;
REMOVE A OR PP IF (-3 Barrera_Inicial_SN LINK NOT 0 N) (-2C N) (-1 CnjCoo) (0 Noun_Adj_PP LINK 0 N OR NP) (1 Barrera_Final_SN) ;
	# entre comunistas y anarquistas

REMOVE A OR PP IF (-1 Barrera_Inicial_SN LINK NOT 0 N) (NOT -1 Limite_Barrera_Inicial_SN) (0 Noun_Adj_PP LINK 0 N OR NP) (1 CM) (2 Noun_Adj_PP LINK 0 N OR NP) (3 CnjCoo) (4 Noun_Adj_PP LINK 0 N OR NP) (5 Barrera_Final_SN) ;
	# entre catalanes, vascos y gallegos

REMOVE A OR PP IF (-1 Barrera_Inicial_SN LINK NOT 0 N) (NOT -1 Limite_Barrera_Inicial_SN) (0 Noun_Adj_PP LINK 0 N OR NP) (1 CM) (2 Noun_Adj_PP LINK 0 N OR NP) (3 CM) (4 Noun_Adj_PP LINK 0 N OR NP) (5 CnjCoo) (6 Noun_Adj_PP LINK 0 N OR NP) (7 Barrera_Final_SN) ;
	# entre occitanos, catalanes, vascos y gallegos
	# vinieron italianos, alemanes, polacos y ucranianos.

SELECT N IF (-2C N) (-1 CnjCoo) (0 Noun_Adj_PP LINK NOT 0 NP) (1 Barrera_Final_Estrict_SN) (NOT 0 ("anche") OR ("secondo") OR ("ancora")) ;
	## adquiriendo una gran reputación como poeta y crítico.
SELECT N IF (2C N) (1 CnjCoo) (0C Noun_Adj_PP LINK NOT 0 NP) (-1 Barrera_Inicial_SN) ;
	## "se utiliza en periódicos y libros"

# NOM, NOM CONJ NOM

SELECT N IF (-2C N) (-1 CM) (0 Noun_Adj_PP) (1 CnjCoo) (2 Noun_Adj_PP) ;
SELECT N IF (-3 CM) (-2C N) (-1 CnjCoo) (0 Noun_Adj_PP) (NOT 1 A) ;
SELECT N IF (0 Noun_Adj_PP) (1 CM) (2C N) (1 CnjCoo) (2 Noun_Adj_PP) ;
SELECT Sg IF (-2C N + Sg) (-1 CM) (0 N + Sg) (1 CnjCoo) (2 N + Sg) ;
SELECT Pl IF (-2C N + Pl) (-1 CM) (0 N + Pl) (1 CnjCoo) (2 N + Pl) ;
SELECT Masc IF (-2C N + Masc) (-1 CM) (0 N + Masc) (1 CnjCoo) (2 N + Masc) ;
SELECT Fem IF (-2C N + Fem) (-1 CM) (0 N + Fem) (1 CnjCoo) (2 N + Fem) ;
SELECT Sg IF (-3 CM) (-2C N + Sg) (-1 CnjCoo) (0 N + Sg) (NOT 1 A) ;
SELECT Pl IF (-3 CM) (-2C N + Pl) (-1 CnjCoo) (0 N + Pl) (NOT 1 A) ;
SELECT Masc IF (-3 CM) (-2C N + Masc) (-1 CnjCoo) (0 N + Masc) (NOT 1 A) ;
SELECT Fem IF (-3 CM) (-2C N + Fem) (-1 CnjCoo) (0 N + Fem) (NOT 1 A) ;
	# compositor, teórico y pedagogo
	# varios religiosos, pensadores y políticos

SELECT N IF (-2 N) (-1 CM) (0 Noun_Adj_PP) (1 CnjCoo) (2C N) ;
SELECT N IF (-2C N) (-1 CM) (0 Noun_Adj_PP) (1 CnjCoo) (2 Det) (3 N) ;
	# Dificil tratar a la vez:
	# ". Católicos, ortodoxos y algunos anglicanos consideran"
	# "Se encuentra en latín, polaco, lituano y otras lenguas."

# ADJ, ADJ CONJ ADJ

SELECT A IF (-2C A) (-1 CM) (0 Noun_Adj_PP) (1 CnjCoo) (2 Noun_Adj_PP) ;
SELECT A IF (-3 CM) (-2C A) (-1 CnjCoo) (0 Noun_Adj_PP) (NOT 1 N) ;
SELECT A IF (0 Noun_Adj_PP) (1 CM) (2C A) (1 CnjCoo) (2 Noun_Adj_PP) ;
	# rojo, verde y azul

# NOM, NOM,

SELECT N IF (-2C N) (-1 CM) (0 Noun_Adj_PP LINK NOT 0 NP) (1 CM OR CnjCoo) ;
SELECT N IF (-4/* Det) (-3C N) (-2 CM) (-1 Det) (0 Noun_Adj_PP LINK NOT 0 NP) (1 CM OR CnjCoo) ;

# ADJ, ADJ,

SELECT A IF (-2C A) (-1 CM) (0 Noun_Adj_PP LINK NOT 0 NP) (1 CM OR CnjCoo) ;

# V
SELECT Vmod IF (1 Inf) ;


REMOVE A IF (NOT -1 Noun_Adj + FemPl OR CnjCoo) (0 ("finas")) ;
REMOVE A IF (NOT -2 Noun_Adj + FemPl) (-1 CnjCoo) (0 ("finas")) ;
SELECT Adv IF (-1C Barrera_Final_Estrict_SN) (0 ("finas")) (1C NP OR Det OR Tn) ;
REMOVE Prep IF (-1 Ser) (0 ("finas") + Adv) (NOT 1 Num) ;	# guess
REMOVE Prep IF (-2 Ser) (-1 PP) (0 ("finas") + Adv) (NOT 1 Num) ;
SELECT Adv IF (-2 CM) (-1 CnjCoo) (0 ("finas")) ;	# , ma finas amigos
SELECT Adv IF (-1C Vlex) (0 ("finas")) ;	# guess
REMOVE Prep IF (-1 CnjCoo) (0 ("finas") + Adv) (NOT 1 Num) ;
REMOVE Prep IF (0 ("finas") + Adv) (1 unknown) ;	# guess
REMOVE Prep IF (0 ("finas") + Adv) ;	# guess
